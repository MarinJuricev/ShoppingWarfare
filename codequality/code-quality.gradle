configurations {
    detekt
}

tasks.register("detekt", JavaExec) {
    main = "io.gitlab.arturbosch.detekt.cli.Main"
    classpath = configurations.detekt

    def input = "$projectDir"
    def config = "$rootDir/codequality/detekt.yml"
    def exclude = ".*/build/.*,.*/resources/.*"
    def params = ['-i', input, '-c', config, '-ex', exclude]

    args(params)
}

dependencies {
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detekt_version"
}

configurations {
    ktlint
}

dependencies {
    ktlint("com.pinterest:ktlint:$ktlint_version") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

task ktlintCheck(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    args ("src/**/*.kt", "--editorconfig=.editorconfig" , "--experimental")
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // to add a baseline to check against prepend following args:
    // "--baseline=ktlint-baseline.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    args ("-F", "src/**/*.kt", "--editorconfig=.editorconfig" , "--experimental")
}